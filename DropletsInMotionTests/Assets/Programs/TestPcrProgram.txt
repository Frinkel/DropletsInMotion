// Input DNA and PCRmix droplets
Droplet(DNA, 0, 0, 1);
Droplet(PCRmix, 0, 19, 1);

// Turn heaters on
Actuator("Heater1", temperature=95);
Actuator("Heater2", temperature=55);
Actuator("Heater3", temperature=72);

// Variables
poolingInterval = 5;
storeTime = 5; // This should be 30 seconds for the real PCR protocol
cycles = 1; // This should be 30 cycles for the real PCR protocol
mixCycles = 1; // This should be 30 cycles for the real PCR protocol
denaturationTemp = 94;
annealingTemp = 54;
extensionTemp = 71;


// Merge DNA and PCRmix
Merge(DNA, PCRmix, mergedDroplet, 2, 7);

// Mix the merges substance
Mix(mergedDroplet, 2, 7, 5, 5, mixCycles);

// Get the temperature of the droplet
s1Temp = Sensor(mergedDroplet, "TempSensor1", "temperature");

// Wait for the droplet to reach the temperature
while(s1Temp < denaturationTemp) {
	Store(mergedDroplet, 12, 16, poolingInterval);
	s1Temp = Sensor(mergedDroplet, "TempSensor1", "temperature");
};
Store(mergedDroplet, 12, 16, storeTime);


// Begin the cycles
i = 0;
while(i < cycles) {
	
	// Denaturation step
	s1Temp = Sensor(mergedDroplet, "TempSensor1", "temperature");
	while(s1Temp < denaturationTemp) {
		Store(mergedDroplet, 12, 16, poolingInterval);
		s1Temp = Sensor(mergedDroplet, "TempSensor1", "temperature");
	}
	Store(mergedDroplet, 12, 16, storeTime);



	// Annealing step
	s2Temp = Sensor(mergedDroplet, "TempSensor2", "temperature");
	while(s2Temp < annealingTemp) {
		Store(mergedDroplet, 12, 3, poolingInterval);
		s2Temp = Sensor(mergedDroplet, "TempSensor2", "temperature");
	}
	Store(mergedDroplet, 12, 3, storeTime);



	// Extension step
	s3Temp = Sensor(mergedDroplet, "TempSensor3", "temperature");
	while(s3Temp < extensionTemp) {
		Store(mergedDroplet, 21, 16, poolingInterval);
		s3Temp = Sensor(mergedDroplet, "TempSensor3", "temperature");
	}
	Store(mergedDroplet, 21, 16, storeTime);

	i = i + 1;
}

// Output
Move(mergedDroplet, 21, 0);
